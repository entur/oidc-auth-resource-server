buildscript {
    configurations.classpath {
        resolutionStrategy {
            // Fix https://github.com/jreleaser/jreleaser/issues/1643
            force 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'
        }
    }
}

plugins {
    id 'base'
    id 'org.jreleaser' version "1.21.0"
    id 'com.github.ben-manes.versions' version '0.53.0' apply false
    id "com.github.spotbugs" version "6.4.5" apply false
    id 'com.diffplug.spotless' version "8.0.0" apply false
}

subprojects {
    ext {
        springBoot3Version = '3.5.7'
        javaJwtVersion = '4.5.0'
        jjwtVersion = '0.13.0'
        apacheCommonsVersion = '2.21.0'
        slf4jVersion = '2.0.17'

        junitPlatformVersion = '1.12.2'
        junitJupiterVersion  = '6.0.1'
        wiremockVersion = '3.13.2'
        logback14xVersion = '1.5.21'

        lombokVersion = '1.18.42'
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                release {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                snapshot {
                    active = 'SNAPSHOT'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    url = "https://ossrh-staging-api.central.sonatype.com/service/local"
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
    release {
        github {
            enabled = true
        }
    }
}